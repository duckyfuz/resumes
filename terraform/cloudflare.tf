# provider "cloudflare" {
#   api_token = var.cloudflare_api_token
# }

locals {
  cloudflare_zone_id = "47e2c26d25d25ff12cb21a0b2a5e1a4d"
  comment_prefix     = "GENERATED BY TERRAFORM"

  acm_certificate_record = tolist(aws_acm_certificate.resume_cert.domain_validation_options)[0]
  acm_certificate_record_name = replace(local.acm_certificate_record.resource_record_name, ".kenf.dev.", "")
  acm_certificate_record_type = local.acm_certificate_record.resource_record_type
  acm_certificate_record_value = substr(local.acm_certificate_record.resource_record_value, 0, length(local.acm_certificate_record.resource_record_value) - 1)
}

resource "cloudflare_record" "acm_certificate_cname" {
  zone_id = local.cloudflare_zone_id
  comment = "${local.comment_prefix} - for AWS ACM certificate"
  name    = local.acm_certificate_record_name
  type    = local.acm_certificate_record_type
  content = local.acm_certificate_record_value
  ttl     = 3600
  proxied = false
}

resource "cloudflare_record" "cloudfront_to_resume_cname" {
  zone_id = local.cloudflare_zone_id
  comment = "${local.comment_prefix} - for cloudfront record"
  name    = "resume"
  type    = "CNAME"
  content = aws_cloudfront_distribution.s3_distribution.domain_name
  ttl     = 3600
  proxied = false
}
